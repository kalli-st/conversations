import com.android.build.OutputFile

// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
    mavenCentral()
}

configurations {
    playstoreImplementation
    compatImplementation
    conversationsFreeCompatImplementation
    conversationsPlaystoreCompatImplementation
    conversationsPlaystoreSystemImplementation
    quicksyFreeCompatImplementation
    quicksyImplementation
}

ext {
    supportLibVersion = '28.0.0'
}

dependencies {
    //should remain that low because later versions introduce dependency to androidx (not sure exactly from what version)
    playstoreImplementation('com.google.firebase:firebase-messaging:17.3.4') {
        exclude group: 'com.google.firebase', module: 'firebase-core'
        exclude group: 'com.google.firebase', module: 'firebase-analytics'
        exclude group: 'com.google.firebase', module: 'firebase-measurement-connector'
    }
    conversationsPlaystoreCompatImplementation("com.android.installreferrer:installreferrer:1.1.2")
    conversationsPlaystoreSystemImplementation("com.android.installreferrer:installreferrer:1.1.2")
    implementation 'org.sufficientlysecure:openpgp-api:10.0'
    implementation('com.theartofdev.edmodo:android-image-cropper:2.7.+') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'exifinterface'
    }
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:exifinterface:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:support-emoji:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    compatImplementation "com.android.support:support-emoji-appcompat:$supportLibVersion"
    conversationsFreeCompatImplementation "com.android.support:support-emoji-bundled:$supportLibVersion"
    quicksyFreeCompatImplementation "com.android.support:support-emoji-bundled:$supportLibVersion"
    implementation 'org.bouncycastle:bcmail-jdk15on:1.64'
    //zxing stopped supporting Java 7 so we have to stick with 3.3.3
    //https://github.com/zxing/zxing/issues/1170
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'de.measite.minidns:minidns-hla:0.2.4'
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'
    implementation 'org.whispersystems:signal-protocol-java:2.6.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation "com.wefika:flowlayout:0.4.1"
    implementation 'net.ypresto.androidtranscoder:android-transcoder:0.3.0'
    implementation 'org.jxmpp:jxmpp-jid:0.6.4'
    implementation 'org.osmdroid:osmdroid-android:6.1.5'
    implementation 'org.hsluv:hsluv:0.2'
    implementation 'org.conscrypt:conscrypt-android:2.2.1'
    implementation 'me.drakeet.support:toastcompat:1.1.0'
    implementation "com.leinardi.android:speed-dial:2.0.1"
    //retrofit needs to stick with 2.6.x (https://github.com/square/retrofit/blob/master/CHANGELOG.md)
    implementation "com.squareup.retrofit2:retrofit:2.6.4"
    implementation "com.squareup.retrofit2:converter-gson:2.6.4"
    //okhttp needs to stick with 3.12.x
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.google.guava:guava:27.1-android'
    quicksyImplementation 'io.michaelrocks:libphonenumber-android:8.11.1'
    //implementation fileTree(include: ['libwebrtc-m87.aar'], dir: 'libs')
    implementation 'org.webrtc:google-webrtc:1.0.32006'
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    preDexEnabled = System.getProperty("pre-dex", "true")
    abiCodes = ['armeabi-v7a': 1, 'x86': 2, 'x86_64': 3, 'arm64-v8a': 4]
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 404
        versionName "2.9.4"
        archivesBaseName += "-$versionName"
        applicationId "eu.sum7.conversations"
        resValue "string", "applicationId", applicationId
        resValue "string", "app_name", "Conv6ations"
        buildConfigField "String", "LOGTAG", "\"conver6ations\""
    }


    dataBinding {
        enabled true
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions("mode", "distribution", "emoji")

    productFlavors {

        quicksy {
            dimension "mode"
            applicationId = "im.quicksy.client"
            resValue "string", "app_name", "Quicksy"
            resValue "string", "applicationId", applicationId
            buildConfigField "String", "LOGTAG", "\"quicksy\""
        }

        conversations {
            dimension "mode"
        }

        playstore {
            dimension "distribution"
        }
        free {
            dimension "distribution"
        }
        system {
            dimension "emoji"
        }
        compat {
            dimension "emoji"
        }
    }

    sourceSets {
        quicksyFreeCompat {
            java {
                srcDir 'src/freeCompat/java'
            }
        }
        quicksyPlaystoreCompat {
            java {
                srcDir 'src/playstoreCompat/java'
            }
            res {
                srcDir 'src/playstoreCompat/res'
                srcDir 'src/quicksyPlaystore/res'
            }
        }
        quicksyPlaystoreSystem {
            res {
                srcDir 'src/quicksyPlaystore/res'
            }
        }
        conversationsFreeCompat {
            java {
                srcDir 'src/freeCompat/java'
                srcDir 'src/conversationsFree/java'
            }
        }
        conversationsFreeSystem {
            java {
                srcDir 'src/conversationsFree/java'
            }
        }
        conversationsPlaystoreCompat {
            java {
                srcDir 'src/playstoreCompat/java'
                srcDir 'src/conversationsPlaystore/java'
            }
            res {
                srcDir 'src/playstoreCompat/res'
                srcDir 'src/conversationsPlaystore/res'
            }
        }
        conversationsPlaystoreSystem {
            java {
                srcDir 'src/conversationsPlaystore/java'
            }
            res {
                srcDir 'src/conversationsPlaystore/res'
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    if (new File("signing.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("signing.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    lintOptions {
        disable 'MissingTranslation', 'InvalidPackage', 'AppCompatResource'
        abortOnError false
    }

    subprojects {

        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    disable 'AndroidGradlePluginVersion', 'MissingTranslation'
                }
            }

        }
    }

    packagingOptions {
        exclude 'META-INF/BCKEY.DSA'
        exclude 'META-INF/BCKEY.SF'
    }

}
